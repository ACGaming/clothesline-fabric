plugins {
    id "fabric-loom" version "0.2.2-SNAPSHOT"
    id "jacoco"
    id "com.matthewprenger.cursegradle" version "1.1.2"
    id "maven-publish"
    id "signing"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = "com.jamieswhiteshirt"
archivesBaseName = "clothesline-fabric"
version = "0.0.16"

repositories {
    maven {
        url "https://maven.jamieswhiteshirt.com/libs-release/"
    }
}

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:1.14 Pre-Release 5"
    mappings "net.fabricmc:yarn:1.14 Pre-Release 5+build.2"
    modCompile "net.fabricmc:fabric-loader:0.4.1+build.126"

    include "com.jamieswhiteshirt:rtree-3i-lite-fabric:0.2.0"

    modCompile "net.fabricmc:fabric:0.2.7+build.123"
    modCompile "com.jamieswhiteshirt:rtree-3i-lite-fabric:0.2.0"
    modCompile "com.jamieswhiteshirt:developer-mode:1.0.11"
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.2.+"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.2.+"
    testCompile "org.mockito:mockito-core:2.+"
}

test {
    useJUnitPlatform()
}

processResources {
    exclude "**/*.psd"
}

jar {
    classifier = "all"
}

task mavenJar(type: Jar) {
    from sourceSets.main.output
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJar, dependsOn: mavenJar) {
    jar = mavenJar.archivePath
    nestJar = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.assemble.dependsOn tasks.remapJar
tasks.assemble.dependsOn tasks.remapMavenJar

project.ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(mavenJar) {
                builtBy remapMavenJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            afterEvaluate {
                pom.withXml {
                    NodeList dependencies = asNode().dependencies

                    // Remove developer-mode dependency (should be modRuntimeOnly)
                    dependencies.'*'.findAll() {
                        it.artifactId.text() == 'developer-mode'
                    }.forEach() {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://maven.jamieswhiteshirt.com/libs-release/'
            def snapshotsRepoUrl = 'https://maven.jamieswhiteshirt.com/libs-snapshot/'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl

            credentials {
                username project.properties.mavenUser
                password project.properties.mavenPassword
            }
        }
    }
}

signing {
    required = isReleaseVersion
    sign publishing.publications.mavenJava
}

curseforge {
    if (project.hasProperty("curseForgeApiKey")) {
        apiKey = project.properties.curseForgeApiKey
    }
    project {
        id = "310078"
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")
        releaseType = "beta"
        addGameVersion "1.14-Snapshot"
        mainArtifact(jar) {
            displayName = "Clothesline $project.version"
            relations {
                requiredDependency "fabric"
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    // CurseGradle generates tasks in afterEvaluate for each project
    // There isn't really any other way to make it depend on a task unless it is an AbstractArchiveTask
    tasks.curseforge310078.dependsOn remapJar
}
