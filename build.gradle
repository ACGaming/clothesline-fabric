import net.fabricmc.loom.task.RemapJar

plugins {
    id "fabric-loom" version "0.2.0-SNAPSHOT"
    id "com.github.johnrengelman.shadow" version "4.0.3"
    id "jacoco"
    id "com.matthewprenger.cursegradle" version "1.1.2"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.jamieswhiteshirt"
archivesBaseName = "clothesline-fabric"
version = "0.0.14"

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "https://maven.jamieswhiteshirt.com/libs-release/"
    }
}

minecraft {
}

configurations {
    contained {
        transitive = false
    }
}

shadowJar {
    configurations = [project.configurations.contained]
}

dependencies {
    minecraft "com.mojang:minecraft:1.14 Pre-Release 5"
    mappings "net.fabricmc:yarn:1.14 Pre-Release 5+build.2"
    modCompile "net.fabricmc:fabric-loader:0.4.1+build.126"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc:fabric:0.2.7+build.123"
    // modCompile "com.jamieswhiteshirt:developer-mode:1.0.10"

    compile "com.jamieswhiteshirt:rtree-3i-lite:0.1-SNAPSHOT"
    contained "com.jamieswhiteshirt:rtree-3i-lite:0.1-SNAPSHOT"
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.2.+"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.2.+"
    testCompile "org.mockito:mockito-core:2.+"
}

test {
    useJUnitPlatform()
}

processResources {
    exclude "**/*.psd"
}

task remapShadowJar(type: RemapJar, dependsOn: tasks.shadowJar) {
    jar = shadowJar.archivePath
}

tasks.assemble.dependsOn tasks.remapJar
tasks.assemble.dependsOn tasks.remapShadowJar

curseforge {
    if (project.hasProperty("curseForgeApiKey")) {
        apiKey = project.properties.curseForgeApiKey
    }
    project {
        id = "310078"
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")
        releaseType = "beta"
        addGameVersion "1.14-Snapshot"
        mainArtifact(remapShadowJar.jar) {
            displayName = "Clothesline $project.version"
            relations {
                requiredDependency "fabric"
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    // CurseGradle generates tasks in afterEvaluate for each project
    // There isn't really any other way to make it depend on a task unless it is an AbstractArchiveTask
    tasks.curseforge310078.dependsOn remapShadowJar
}
